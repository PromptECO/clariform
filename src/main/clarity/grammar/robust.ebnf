S = <OWS> (!ws toplevel <OWS>)*
toplevel = form | !form ( !WS bypass / forward)  (* emulate reader to fail on individual toplevel forms *)
<safe-form> = form | !form !WS skip
<form> = list | vector | record | atom
list = <LPAR> <OWS> (!RPAR safe-form <OWS>)* <RPAR>
vector = <LBRA> <OWS> (!<RBRA> form <OWS>)* <RBRA>
record = <LCUR> <OWS> (&RCUR missing | !RCUR content) <RCUR>
<content> = (!ws !RCUR !COMMA prop <OWS> (<COMMA> <OWS>)? )+
prop = name <OWS>
        (<COLON> <OWS> (form | !form missing)
        |&RCUR 
        |&COMMA 
        |!(COLON|RCUR|COMMA) (form | !form missing))
     | !name (<COLON> <OWS> missing form 
             |!COLON (&form missing form 
                     |!form skip (safe-form / missing))) 

<name> = !operator symbol
<OWS> = #'([\s]+|(;[^\n]*[\n]?))*'
<WS> = #'([\s]+|(;[^\n]*[\n]?))+'
<bypass> =  skip | !skip invalid  (* for toplevel *)
<forward> = (#'[^\n]*\n')* &LPAR  (* skips to next toplevel *) 
skip = #'[^\s\(\)\{\}\[\]\;\\"]+' 
invalid = #'[^\s]'
missing = Epsilon

LBRA = '['
RBRA = ']'

bracket = #'[\(\{\[\)\}\]]'
<terminator> = #'[\s\)\}\]]'

<ws1> = #'[\s]+' | comment
comment = #';;[^\n$]*[\n]?'
